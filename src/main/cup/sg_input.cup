package wanghg.rhigosis;

import scala.collection.mutable.ListBuffer;

terminal ID;
terminal LHS;
terminal BAR;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal LPAREN;
terminal RPAREN;
terminal LITERAL;

nonterminal grammar;
nonterminal prod;
nonterminal rhs;
nonterminal prod_list;
nonterminal term;
nonterminal sequence;
nonterminal options;
nonterminal group;
nonterminal zero_or_one;
nonterminal zero_or_more;

start with grammar;

grammar         ::= prod_list:pl {:
    RESULT = SGUtils.grammar((ListBuffer<Production>)pl);
:};

prod_list       ::= prod:p {:
    RESULT = SGUtils.listBuffer(p);
:}
                  | prod_list:g prod:p {:
    RESULT = SGUtils.listBuffer((ListBuffer<Production>)g, (Production)p);
:};

prod            ::= LHS:id rhs:r {:
    RESULT = SGUtils.production((String)id, (Rhs)r);
:};

rhs             ::= options:o {:
    ListBuffer<Concatenation> os = (ListBuffer<Concatenation>)o;
    RESULT = SGUtils.rhs(SGUtils.alternation(os));
:}
                  | options:o BAR {:
    ListBuffer<Concatenation> os =
        SGUtils.listBuffer((ListBuffer<Concatenation>)o, SGUtils.concatenation());
    RESULT = SGUtils.rhs(SGUtils.alternation(os));
:};

options         ::= sequence:s {:
    RESULT = SGUtils.listBuffer(SGUtils.concatenation((ListBuffer<Term>)s));
:}
                  | options:o BAR sequence:s {:
    RESULT = SGUtils.listBuffer((ListBuffer<Concatenation>)o, SGUtils.concatenation((ListBuffer<Term>)s));
:};

sequence        ::= term:t {:
    RESULT = SGUtils.listBuffer((Term)t);
:}
                  | sequence:s term:t {:
    RESULT = SGUtils.listBuffer((ListBuffer<Term>)s, (Term)t);
:};

term            ::= ID:id {:
    RESULT = SGUtils.id((String)id);
:}
                  | LITERAL:lit {:
    RESULT = SGUtils.literal((String)lit);
:}
                  | group:g {:
    RESULT = g;
:}
                  | zero_or_one:zoo {:
    RESULT = zoo;
:}
                  | zero_or_more:zom {:
    RESULT = zom;
:};

group           ::= LPAREN options:o RPAREN {:
    RESULT = SGUtils.grouping(SGUtils.alternation((ListBuffer<Concatenation>)o));
:};

zero_or_one     ::= LBRACK options:o RBRACK {:
    RESULT = SGUtils.optional(SGUtils.alternation((ListBuffer<Concatenation>)o));
:};

zero_or_more    ::= LBRACE options:o RBRACE {:
    RESULT = SGUtils.repetition(SGUtils.alternation((ListBuffer<Concatenation>)o));
:};



























