package wanghg.rhigosis;

terminal    AT;
terminal    CHAR;
terminal    COLON;
terminal    COMMA;
terminal    DOT;
terminal    EQ;
terminal    ID;
terminal    INT;
terminal    LARROW;
terminal    LBRACK;
terminal    LBRACE;
terminal    LPAREN;
terminal    MCOMMENT;
terminal    MSTRING;
terminal    NEWLINE;
terminal    PACKAGE;
terminal    RBRACK;
terminal    RBRACE;
terminal    RDARROW;
terminal    RPAREN;
terminal    SCOMMENT;
terminal    SEMICOLON;
terminal    STRING;
terminal    SYMBOL;
terminal    ABSTRACT;
terminal    CLASS;
terminal    PRIVATE;
terminal    THIS;
terminal    EXTENDS;
terminal    WITH;
terminal    TRAIT;
terminal    IMPORT;
terminal    UNDERSCORE;
terminal    FINAL;
terminal    VAL;
terminal    VAR;
terminal    OBJECT;

nonterminal CompilationUnit;
nonterminal QualId;
nonterminal TopStatSeq;
nonterminal TopStat;
nonterminal Modifier;
nonterminal LocalModifier;
nonterminal TmplDef;
nonterminal ClassDef;
nonterminal ClassTemplateOpt;
nonterminal TemplateBody;
nonterminal Annotation;
nonterminal SimpleType;
nonterminal StableId;
nonterminal ArgumentExprs;
nonterminal Exprs;
nonterminal Expr;
nonterminal Literal;
nonterminal AccessModifier;
nonterminal AccessQualifier;
nonterminal ClassTemplate;
nonterminal ClassParents;
nonterminal Constr;
nonterminal AnnotType;
nonterminal Path;
nonterminal TraitDef;
nonterminal TmplDef_withAnnotation;
nonterminal TmplDef_withModifier;
nonterminal TraitTemplateOpt;
nonterminal TraitTemplate;
nonterminal TraitParents;
nonterminal Import;
nonterminal ImportExpr;
nonterminal ClassParamClauses;
nonterminal ClassParamClauseList;
nonterminal ClassParamClause;
nonterminal ClassParams;
nonterminal ClassParam;
nonterminal ParamType;
nonterminal Type;
nonterminal InfixType;
nonterminal CompoundType;
nonterminal ClassParam_withAnnotation;
nonterminal ClassParam_withModifier;
nonterminal ObjectDef;
nonterminal TemplateStatList;
nonterminal TemplateStat;
nonterminal Def;
nonterminal PatVarDef;
nonterminal PatDef;
nonterminal Pattern2List;
nonterminal Pattern2;
nonterminal Def_withAnnotation;
nonterminal Def_withModifier;

CompilationUnit             ::= PACKAGE QualId TopStatSeq;

TopStatSeq                  ::= TopStat
                              | TopStatSeq TopStat
                              ;

TopStat                     ::= TmplDef_withAnnotation
                              | Import
                              ;

Import                      ::= IMPORT ImportExpr
                              ;

ImportExpr                  ::= StableId
                              | StableId DOT UNDERSCORE
                              ;

TmplDef_withModifier        ::= TmplDef
                              | Modifier TmplDef_withModifier
                              ;

TmplDef_withAnnotation      ::= TmplDef_withModifier
                              | Annotation TmplDef_withAnnotation
                              ;

Modifier                    ::= LocalModifier
                              | AccessModifier
                              ;

LocalModifier               ::= ABSTRACT
                              | FINAL
                              ;

QualId                      ::= ID
                              | QualId DOT ID
                              ;

TmplDef                     ::= CLASS ClassDef
                              | OBJECT ObjectDef
                              | TRAIT TraitDef
                              ;

ObjectDef                   ::= ID ClassTemplateOpt
                              ;

TraitDef                    ::= ID TraitTemplateOpt
                              ;

TraitTemplateOpt            ::= EXTENDS TraitTemplate
                              ;

TraitTemplate               ::= TraitParents
                              ;

TraitParents                ::= AnnotType
                              ;

ClassDef                    ::= ID ClassParamClauses ClassTemplateOpt;

ClassParamClauses           ::=
                              | ClassParamClauseList
                              ;

ClassParamClauseList        ::= ClassParamClause
                              | ClassParamClauseList ClassParamClause
                              ;

ClassParamClause            ::= LPAREN ClassParams RPAREN
                              ;

ClassParams                 ::=
                              | ClassParam_withAnnotation
                              | ClassParams COMMA ClassParam_withAnnotation
                              ;

ClassParam                  ::= ID COLON ParamType
                              | VAR ID COLON ParamType
                              | VAL ID COLON ParamType
                              ;

ClassParam_withModifier     ::= ClassParam
                              | Modifier ClassParam_withModifier
                              ;

ClassParam_withAnnotation   ::= ClassParam_withModifier
                              | Annotation ClassParam_withAnnotation
                              ;

ParamType                   ::= Type
                              ;

Type                        ::= InfixType
                              ;

InfixType                   ::= CompoundType
                              ;

CompoundType                ::= AnnotType
                              ;

ClassTemplateOpt            ::= EXTENDS ClassTemplate
                              | TemplateBody
                              ;

ClassTemplate               ::= ClassParents;

ClassParents                ::= Constr
                              | ClassParents WITH AnnotType
                              ;

Constr                      ::= AnnotType;

AnnotType                   ::= SimpleType;

TemplateBody                ::= LBRACE RBRACE
                              | LBRACE TemplateStatList RBRACE
                              ;

TemplateStatList            ::= TemplateStat
                              | TemplateStatList TemplateStat
                              ;

TemplateStat                ::= Def_withAnnotation
                              ;

Def                         ::= PatVarDef
                              ;

Def_withModifier            ::= Def
                              | Modifier Def_withModifier
                              ;

Def_withAnnotation          ::= Def_withModifier
                              | Annotation Def_withAnnotation
                              ;

PatVarDef                   ::= VAL PatDef
                              ;

PatDef                      ::= Pattern2List EQ Expr
                              | Pattern2List COLON Type EQ Expr
                              ;

Pattern2List                ::= Pattern2
                              | Pattern2List COMMA Pattern2
                              ;

Pattern2                    ::= ID
                              ;

Annotation                  ::= AT SimpleType
                              | Annotation ArgumentExprs
                              ;

SimpleType                  ::= StableId
                              ;

StableId                    ::= ID
                              | StableId DOT ID
                              | THIS DOT ID
                              | StableId DOT THIS DOT ID
                              ;

ArgumentExprs               ::= LPAREN RPAREN
                              | LPAREN Exprs RPAREN
                              ;

Exprs                       ::= Expr
                              | Exprs COMMA Expr
                              ;

Expr                        ::= Literal
                              | StableId
                              ;

Literal                     ::= STRING
                              | INT
                              ;

AccessModifier              ::= PRIVATE
                              | PRIVATE AccessQualifier
                              ;

AccessQualifier             ::= LBRACK THIS RBRACK
                              | LBRACK ID RBRACK
                              ;

