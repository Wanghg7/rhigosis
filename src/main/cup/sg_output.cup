package wanghg.rhigosis;

terminal ID;
terminal LHS;
terminal BAR;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal LPAREN;
terminal RPAREN;
terminal LITERAL;

nonterminal grammar;
nonterminal prod;
nonterminal rhs;
nonterminal term;
nonterminal options;

start with grammar;

grammar     ::= prod:p {:
    RESULT = String.format("%s\n", p);
:};
grammar     ::= grammar:g prod:p {:
    RESULT = String.format("%s\n%s\n", g, p);
:};

prod        ::= LHS:id rhs:r  {:
    RESULT = String.format("%24s ::= %s\n", id, r);
:};
prod        ::= prod:p BAR rhs:r  {:
    RESULT = String.format("%s%28s %s\n", p, "|", r);
:};
prod        ::= prod:p BAR   {:
    RESULT = String.format("%s%28s \n", p, "|");
:};

rhs         ::= term:t {:
    RESULT = String.format("%s", t);
:};
rhs         ::= rhs:r term:t {:
    RESULT = String.format("%s %s", r, t);
:};

term        ::= LBRACK options:o RBRACK {:
    RESULT = String.format("[%s]", o);
:};
term        ::= LBRACE options:o RBRACE {:
    RESULT = String.format("{%s}", o);
:};
term        ::= LPAREN options:o RPAREN {:
    RESULT = String.format("(%s)", o);
:};
term        ::= LITERAL:lit {:
    RESULT = String.format("%s", lit);
:};
term        ::= ID:id {:
    RESULT = String.format("%s", id);
:};

options     ::= rhs:r {:
    RESULT = String.format("%s", r);
:};
options     ::= options:o BAR rhs:r {:
    RESULT = String.format("%s | %s", o, r);
:};












