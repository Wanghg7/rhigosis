package wanghg.rhigosis;

// TERMINAL
terminal PACKAGE;
terminal SEMI;
terminal ID;
terminal DOT;
terminal NL;
terminal IMPORT;
terminal COMMA;
terminal LBRACE;
terminal RBRACE;
terminal OBJECT;
terminal CLASS;
terminal CASE;
terminal TRAIT;
terminal OVERRIDE;
terminal AT;
terminal UNDERSCORE;
terminal ABSTRACT;
terminal FINAL;
terminal SEALED;
terminal IMPLICIT;
terminal LAZY;
terminal PRIVATE;
terminal PROTECTED;
terminal HASH;
terminal TYPE;
terminal LPAREN;
terminal RPAREN;
terminal COLON;
terminal ASTERISK;
terminal SUPER;
terminal EXTENDS;
terminal LBRACK;
terminal RBRACK;
terminal THIS;
terminal RDARROW;
terminal NL_LPAREN;
terminal NL_LBRACE;
terminal PLUS;
terminal MINUS;
// TODO
terminal CaseClauses;
terminal Block;
terminal ClassParents;
terminal EarlyDefs;
terminal SelfType;
terminal TemplateStat;
terminal TypeParam;
terminal ClassParam;
terminal TraitParents;
terminal FunctionArgTypes;
terminal InfixType;
terminal ExistentialClause;
terminal Bindings;
terminal PrefixExpr;
// DONE
nonterminal CompilationUnit;
nonterminal TopStatSeq;
nonterminal QualId;
nonterminal TopStat;
nonterminal _AN_M_TmplDef;
nonterminal _M_TmplDef;
nonterminal Import;
nonterminal Packaging;
nonterminal PackageObject;
nonterminal TmplDef;
nonterminal Modifier;
nonterminal Annotation;
nonterminal ImportExpr;
nonterminal ObjectDef;
nonterminal ClassDef;
nonterminal _A_ClassParamClauses;
nonterminal TraitDef;
nonterminal LocalModifier;
nonterminal AccessModifier;
nonterminal SimpleType;
nonterminal ArgumentExprs;
nonterminal StableId;
nonterminal ImportSelectors;
nonterminal _ImportSelectors;
nonterminal ClassTemplateOpt;
nonterminal TypeParamClause;
nonterminal _TypeParamClause;
nonterminal ClassParamClauses;
nonterminal ConstrAnnotation;
nonterminal TraitTemplateOpt;
nonterminal AccessQualifier;
nonterminal TypeArgs;
nonterminal Path;
nonterminal Types;
nonterminal Exprs;
nonterminal PostfixExpr;
nonterminal BlockExpr;
nonterminal ClassQualifier;
nonterminal ImportSelector;
nonterminal ClassTemplate;
nonterminal TemplateBody;
nonterminal _TemplateBody;
nonterminal VariantTypeParam;
nonterminal ClassParams;
nonterminal ClassParamClause;
nonterminal TraitTemplate;
nonterminal Type;
nonterminal Expr;
nonterminal InfixExpr;
// Precedence
precedence left ID/*used in InfixExpr*/;
precedence left DOT/*used in StableId*/;
// Rules

CompilationUnit ::=  TopStatSeq;
CompilationUnit ::=  PACKAGE QualId    SEMI CompilationUnit;

TopStatSeq        ::=  TopStat;
TopStatSeq        ::=  TopStatSeq SEMI TopStat;

QualId            ::=  ID;
QualId            ::=  QualId DOT ID;

TopStat           ::=  _AN_M_TmplDef;
TopStat           ::=  Import;
TopStat           ::=  Packaging;
TopStat           ::=  PackageObject;
TopStat           ::=  ;

_M_TmplDef          ::= TmplDef;
_M_TmplDef          ::= Modifier _M_TmplDef;

_AN_M_TmplDef       ::= _M_TmplDef;
_AN_M_TmplDef       ::= Annotation _AN_M_TmplDef;
_AN_M_TmplDef       ::= Annotation NL _AN_M_TmplDef;

Import            ::=  IMPORT ImportExpr;
Import            ::=  Import COMMA ImportExpr;

Packaging         ::=  PACKAGE QualId LBRACE TopStatSeq RBRACE;
Packaging         ::=  PACKAGE QualId NL LBRACE TopStatSeq RBRACE;

PackageObject     ::=  PACKAGE OBJECT ObjectDef;

TmplDef           ::=  CLASS ClassDef;
TmplDef           ::=  CASE CLASS ClassDef;
TmplDef           ::=  OBJECT ObjectDef;
TmplDef           ::=  CASE OBJECT ObjectDef;
TmplDef           ::=  TRAIT TraitDef;

Modifier          ::=  LocalModifier;
Modifier          ::=  AccessModifier;
Modifier          ::=  OVERRIDE;

Annotation        ::=  AT SimpleType;
Annotation        ::=  Annotation ArgumentExprs;

ImportExpr        ::=  StableId DOT ID;
ImportExpr        ::=  StableId DOT UNDERSCORE;
ImportExpr        ::=  StableId DOT ImportSelectors;

ObjectDef         ::=  ID ClassTemplateOpt;

ClassDef          ::=  ID _A_ClassParamClauses ClassTemplateOpt;
ClassDef          ::=  ID TypeParamClause _A_ClassParamClauses ClassTemplateOpt;

_A_ClassParamClauses    ::= ClassParamClauses;
_A_ClassParamClauses    ::= AccessModifier ClassParamClauses;
_A_ClassParamClauses    ::= ConstrAnnotation _A_ClassParamClauses;

TraitDef          ::=  ID TraitTemplateOpt;
TraitDef          ::=  ID TypeParamClause TraitTemplateOpt;

LocalModifier     ::=  ABSTRACT;
LocalModifier     ::=  FINAL;
LocalModifier     ::=  SEALED;
LocalModifier     ::=  IMPLICIT;
LocalModifier     ::=  LAZY;

AccessModifier    ::=  PRIVATE;
AccessModifier    ::=  PRIVATE AccessQualifier;
AccessModifier    ::=  PROTECTED;
AccessModifier    ::=  PROTECTED AccessQualifier;

SimpleType        ::=  SimpleType TypeArgs;
SimpleType        ::=  SimpleType HASH ID;
SimpleType        ::=  StableId;
SimpleType        ::=  Path DOT TYPE;
SimpleType        ::=  LPAREN Types RPAREN;

ArgumentExprs     ::=  LPAREN RPAREN;
ArgumentExprs     ::=  LPAREN Exprs RPAREN;
ArgumentExprs     ::=  LPAREN PostfixExpr COLON UNDERSCORE ASTERISK RPAREN;
ArgumentExprs     ::=  LPAREN Exprs COMMA PostfixExpr COLON UNDERSCORE ASTERISK RPAREN;
ArgumentExprs     ::=  BlockExpr;
ArgumentExprs     ::=  NL BlockExpr;

StableId          ::=  ID;
StableId          ::=  Path DOT ID;
StableId          ::=  SUPER DOT ID;
StableId          ::=  SUPER ClassQualifier DOT ID;
StableId          ::=  ID DOT SUPER DOT ID;
StableId          ::=  ID DOT SUPER ClassQualifier DOT ID;

ImportSelectors   ::=  LBRACE _ImportSelectors RBRACE;

_ImportSelectors    ::= ImportSelector;
_ImportSelectors    ::= UNDERSCORE;
_ImportSelectors    ::= ImportSelector COMMA _ImportSelectors;

ClassTemplateOpt  ::= EXTENDS ClassTemplate;
ClassTemplateOpt  ::= TemplateBody;
ClassTemplateOpt  ::= EXTENDS TemplateBody;

TypeParamClause   ::=  LBRACK _TypeParamClause RBRACK;

_TypeParamClause    ::= VariantTypeParam;
_TypeParamClause    ::= _TypeParamClause COMMA VariantTypeParam;

ClassParamClauses ::=  ;
ClassParamClauses ::=  LPAREN IMPLICIT ClassParams RPAREN;
ClassParamClauses ::=  NL_LPAREN IMPLICIT ClassParams RPAREN;
ClassParamClauses ::=  ClassParamClause ClassParamClauses;

ConstrAnnotation  ::=  AT SimpleType ArgumentExprs;

TraitTemplateOpt  ::=  ;
TraitTemplateOpt  ::=  TemplateBody;
TraitTemplateOpt  ::=  EXTENDS TemplateBody;
TraitTemplateOpt  ::=  EXTENDS TraitTemplate;

AccessQualifier   ::=  LBRACK ID RBRACK;
AccessQualifier   ::=  LBRACK THIS RBRACK;

TypeArgs          ::=  LBRACK Types RBRACK;

Path              ::=  StableId;
Path              ::=  THIS;
Path              ::=  ID DOT THIS;

Types             ::=  Type;
Types             ::=  Types COMMA Type;

Exprs             ::=  Expr;
Exprs             ::=  Exprs COMMA Expr;

PostfixExpr       ::=  InfixExpr;
PostfixExpr       ::=  InfixExpr ID;
PostfixExpr       ::=  InfixExpr ID NL;

BlockExpr         ::=  LBRACE CaseClauses RBRACE;
BlockExpr         ::=  LBRACE Block RBRACE;

ClassQualifier    ::=  LBRACK ID RBRACK;

ImportSelector    ::=  ID;
ImportSelector    ::=  ID RDARROW ID;
ImportSelector    ::=  ID RDARROW UNDERSCORE;

ClassTemplate     ::=  ClassParents;
ClassTemplate     ::=  ClassParents TemplateBody;
ClassTemplate     ::=  EarlyDefs ClassParents;
ClassTemplate     ::=  EarlyDefs ClassParents TemplateBody;

TemplateBody      ::=  LBRACE _TemplateBody RBRACE;
TemplateBody      ::=  LBRACE SelfType _TemplateBody RBRACE;
TemplateBody      ::=  NL_LBRACE _TemplateBody RBRACE;
TemplateBody      ::=  NL_LBRACE SelfType _TemplateBody RBRACE;

_TemplateBody       ::= TemplateStat;
_TemplateBody       ::= _TemplateBody SEMI TemplateStat;

VariantTypeParam  ::=  TypeParam;
VariantTypeParam  ::=  PLUS TypeParam;
VariantTypeParam  ::=  MINUS TypeParam;
VariantTypeParam  ::=  Annotation VariantTypeParam;

ClassParams       ::=  ClassParam;
ClassParams       ::=  ClassParams COMMA ClassParam;

ClassParamClause  ::=  LPAREN RPAREN;
ClassParamClause  ::=  LPAREN ClassParams RPAREN;
ClassParamClause  ::=  NL LPAREN RPAREN;
ClassParamClause  ::=  NL LPAREN ClassParams RPAREN;

TraitTemplate     ::=  TraitParents;
TraitTemplate     ::=  TraitParents TemplateBody;
TraitTemplate     ::=  EarlyDefs TraitParents;
TraitTemplate     ::=  EarlyDefs TraitParents TemplateBody;

Type              ::=  FunctionArgTypes RDARROW Type;
Type              ::=  InfixType;
Type              ::=  InfixType ExistentialClause;

Expr              ::=  Bindings RDARROW Expr;
Expr              ::=  ID RDARROW Expr;
Expr              ::=  IMPLICIT ID RDARROW Expr;
Expr              ::=  UNDERSCORE RDARROW Expr;

InfixExpr         ::=  PrefixExpr;
InfixExpr         ::=  InfixExpr ID InfixExpr;
InfixExpr         ::=  InfixExpr ID NL InfixExpr;


